plugins {
    id "org.sonarqube" version "2.7"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply from: 'generateVersion.gradle'
apply from: 'sourceDirectories.gradle'

android {

    def generatedVersionName = generateVersionName()

    compileSdkVersion app.compileSdk
    buildToolsVersion app.buildTools

    defaultConfig {
        applicationId "com.kaloglu.bedavanevar"

        minSdkVersion app.minSdk
        targetSdkVersion app.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        versionCode 1
        versionName generatedVersionName

        multiDexEnabled true

    }

    signingConfigs {
        clientBeta {
            storeFile file('.betakey')
            keyAlias '.betakey'
            keyPassword '.betakey'
            storePassword '.betakey'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.clientBeta
        }
        beta {
            debuggable true
            signingConfig signingConfigs.clientBeta
            ext.betaDistributionGroupAliases = "beta-android"
            ext.betaDistributionReleaseNotesFilePath = file('../beta_release_notes.txt').absolutePath
            applicationIdSuffix ".beta"
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility java.sourceCompatibility
        targetCompatibility java.targetCompatibility
    }

    flavorDimensions 'default'
    productFlavors {
        client {
            dimension 'default'
            buildConfigField 'String', 'NETWORK_PROTOCOL', '"firestore"'
        }

    }
    sourceSets {
        main {
            res.srcDirs = getSourceDirectories(['main'])
            java.srcDirs = ['src/main/kotlin']
        }

    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    addDependencies(dependencies, appDependencies)
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.0.2'
}

apply plugin: 'com.google.gms.google-services'


task postBeta {
    doLast {
        incrementVersion()
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        def flavor = variant.productFlavors[0].name.capitalize()
        outputFileName = "BedavaNeVar-${flavor}-${variant.versionName}.apk"
    }
}
